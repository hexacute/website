---
date: 2017-10-02
title: "Introduction √† ReasonML (ou pourquoi j'ai arr√™t√© d'utiliser JavaScript)"
author: bloodyowl
oldSlug: reason/introduction-reason
slug: introduction-a-reasonml
---

Les bugs c'est chiant, surtout quand on sait qu'on aurait pu les √©viter. Avec
JavaScript, c'est plus facile d'en avoir que de les √©viter.

JavaScript, on ne peut pas y √©chapper parce que c'est partout, parce que c'est
le langage de programmation le plus utilis√© sur Terre, et parce que c'est le
seul truc qui (pour l'instant) tourne nativement dans le navigateur. Et puis
c'est pas si mal, c'est facile de l'apprendre en bidouillant un peu, c'est tr√®s
permissif et pas trop frustrant pour d√©buter.

Le probl√®me c'est que c'est facile d'en faire, mais c'est tr√®s dur d'en faire
**bien** (moins que CSS, mais quand m√™me). On s'est tous d√©j√† retrouv√© avec
`undefined is not a function`, `null is not an object` ou un bon gros
`[object Object]` qui tra√Æne dans la console.

## Pourquoi est-ce que c'est comme √ßa ?

JavaScript est un langage dynamiquement et faiblement typ√©, ce qui veut dire :

- qu'une fonction ne sait pas ce qu'elle prend ou retourne comme type
  d'arguments, c'est √† vous de g√©rer
- que le programme n'en a aucune id√©e non plus tant qu'il n'execute pas la
  portion de code

Tout √ßa fait qu'il est tr√®s difficile de faire confiance √† du code JavaScript.

<figure>
<img src="/public/images/articles/2017-10-02-introduction-a-reasonml/js.png" alt="js" />
<figcaption>Source: MIT</figcaption>
</figure>

Il existe Flow et TypeScript (dont on a parl√© dans un
[pr√©c√©dent article](/fr/articles/js/flow/) et un
[podcast](fr/articles/podcast/3/)), deux projets qui permettent d'apporter du
typage statique pour s√©curiser son code. Ils contraignent votre usage de
JavaScript, mais devront toujours se battre contre sa permissivit√©.

√áa revient au final √† coller des rustines sur vos pneus avant d'aller rouler sur
des clous, √ßa va vous prot√©ger un peu, mais √ßa reste de base pas bien malin
d'aller rouler sur des clous.

Il existe des langages qui ont la judicieuse id√©e de balayer la route pour virer
les clous avant d'y aller : les langages typ√©s fortement et statiquement (10
points pour Gryffondor pour cette m√©taphore fil√©e).

OCaml est un de ces langages. Il est de la famille ML et a √©t√© cr√©√© en France
dans les ann√©es 90. Il est √† peu pr√®s aussi √¢g√© que JavaScript mais est beaucoup
plus sage. Il est certes fortement et statiquement typ√©, mais il inf√®re la
plupart des types du programme (ce qui veut dire que vous n'avez pas √†
renseigner les types partout, il va l'extrapoler d√®s qu'il le peut). En bonus,
il poss√®de de
[bonnes data-structures](https://ocaml.org/learn/tutorials/comparison_of_standard_containers.html).

Seulement voil√†, OCaml, comme beaucoup de langages fonctionnels n'a pas vraiment
mis l'accent sur l'accessibilit√© pour les d√©butants, et a une syntaxe qu'on peut
pour le moins qualifier de pas tr√®s friendly: elle n'a rien de bien mal, mais
mettez quelqu'un qui vient de JS/PHP/Java devant, √ßa va pas lui causer des
masses:

```ocaml
let rec qsort = fun value ->
  match value with
   | [] -> []
   | pivot :: rest ->
       let is_less x = x < pivot in
       let left, right = List.partition is_less rest in
       qsort left @ [pivot] @ qsort right;;
```

<figure>
<img src="/public/images/articles/2017-10-02-introduction-a-reasonml/giphy.gif" alt="" />
<figcaption>fig. 1: dev JS devant du code OCaml</figcaption>
</figure>

C'est l√† qu'intervient Reason, un projet initi√© par
[le cr√©ateur de React](https://twitter.com/jordwalke). Les premi√®res it√©rations
de React √©taient d'ailleurs cod√©es dans un langage cousin de OCaml, le SML.

C'est en gros:

- OCaml
- avec une syntaxe _beginner-friendly_
- avec un tooling le rendant plus simple √† utiliser
- qui peut compiler vers JavaScript et de multiples plateformes

Avec son type system, OCaml propose un langage _safe-by-design_, chose que m√™me
avec une palanqu√©e d'outils, on ne peut pas atteindre avec JavaScript.

Le langage propose par d√©faut une approche fonctionnelle et immutable, mais
permet cependant de choisir d'utiliser des structures mutables et de l'orient√©
objet au besoin.

## Le langage

Reason comporte plus de types de primitifs que JavaScript:

```reason
/* pas un gros "number" fourre tout, magique */
1; /* int */
1.0; /* float */
/* un caract√®re est d'un type diff√©rent de string */
"foo"; /* string */
'a'; /* char */
/* on trouve list ET array, chacun peut √™tre utilis√© pour diff√©rents cas */
[1, 2, 3]; /* list */
[| 1, 2, 3 |]; /* array */
/* pas de null, mais des valeurs de type `option` qui contiennent
  soit une valeur, soit rien */
Some(1); /* option int */
None; /* option int */
```

Puisqu'il est fortement typ√©, il est impossible de mixer les types comme en
JavaScript, vous devrez obligatoirement les convertir:

```reason
1 + 1.0;
/* Error:
  This expression has type int but an expression was expected of type float */
1 + int_of_float(1.0);
/* - : int = 2 */
```

La plupart des op√©rations de transformations de type primitifs vers un autre
sont accessibles dans le module `Pervasives` qui contient plein de petits
utilitaires bien pratiques. Toutes les fonctions de ce module sont accessibles
directement dans n'importe quel de vos fichiers.

Les fonctions de Reason sont beaucoup plus puissantes qu'en JavaScript:

```reason
let add = (a, b) => a + b;
/* int => int => int */
add(1, 2);
/* 3 */
add(1, 2.0);
/* This expression has type float but an expression was expected of type int */

/* Les fonctions sont "auto-curried", ce qui signifie qu'une fonction
   qui n'a pas re√ßu tous ses param√®tres retourne une nouvelle fonction
  qui va recevoir les param√®tres manquants */
let addOne = add(1);
/* int => int */

/* Les fonctions peuvent avoir des param√®tres nomm√©s, optionnels et avec des valeurs par d√©faut */
let sayHi = (~name, ~punct="!", ()) => "Hello " ++ name ++ punct;
/* name::string => string */
sayHi(~name="you", ());
/* "Hello you!" */
/* L'ordre des arguments nomm√©s n'a pas d'importance*/
sayHi(~punct="?", ~name="you", ());
/* "Hello you?" */
```

Pour d√©finir l'√©quivalent d'un _plain-object_ JavaScript en Reason, on utilise
des records:

```reason
/* On doit typer les records */
type user = {
  username: string,
  age: int
};

/* Les records ont un nombre de cl√©s fini, vous ne pouvez pas en
  ommettre une ou en mettre une en trop */
let user = { username: "Bob" };
/* Error: Some record fields are undefined: age */

let user = { username: "Bob", age: 20 };

/* Les records sont immutables par d√©faut */
let olderUser = {...user, age: user.age + 1 };
```

Reason poss√®de un syst√®me de module tr√®s puissant: par d√©faut, chaque fichier de
votre codebase est un module, mais vous pouvez √©galement d√©clarer des modules
_dans_ un module.

```reason
module User = {
  type t = {
    id: string,
    username: string,
    email: option(string)
  };
  let make = (~id, ~username, ~email) => {id, username, email};
  let sayHi = (user) => "Hello " ++ user.username ++ "!";
};

/* Pour utiliser un module, tapez son nom, tout simplement */
User.make(~id="0", ~username="bloodyowl", ~email=None)
  /* (ah oui, le pipe existe d√©j√† ici, pas besoin d'attendre ES2050)*/
  |> User.sayHi
  |> print_endline;
/* "Hello bloodyowl" */

/* On peut √©galement rendre toutes les valeurs d'un module accessibles localement */
User.(
  make(~id="0", ~username="bloodyowl", ~email=None)
    |> sayHi
    |> print_endline
);

/* Carr√©ment les rendre accessibles globalement dans le module */
open User;

make(~id="0", ~username="bloodyowl", ~email=None)
  |> sayHi
  |> print_endline;

/* Ou m√™me √©tendre un module statiquement et proprement,
  √ßa vous parle √ßa, Prototype et MooTools ?! */
module UserThatCanSayBye = {
  include User;
  let sayBye = (user) => "Bye " ++ user.username ++ "!";
};
```

Il existe √©galement des functors, qui sont des sortes de fonctions retournant
des modules √† partir d'autres modules, mais on ne l'abordera pas dans cet
article.

Reason poss√®de √©galement des variants, il s'agit de types pouvant avoir
diff√©rents cas. Prenons l'exemple d'un message de chat:

```reason
type image = {url: string, width: int, height: int};

/* chaque cas du variant peut prendre des param√®tres */
type message =
  | String(string) /* soit une cha√Æne de caract√®res */
  | Image(image) /* soit une image */
  | Emoji(string); /* soit un gros emoji */

let stringMessage = String("Hello"); /* On cr√©e la valeur avec son constructeur */
let imageMessage = Image({url: "https://fakeimg.pl/300x300", width: 300, height: 300});
let emojiMessage = Emoji({js|üê´|js}); /* Quand la string contient de caract√®res unicode,
  on doit utiliser {js|votre string|js} */
```

Ici, notre type `message` est bien d√©limit√©, et ses valeurs ne peuvent √™tre que
celles que l'on a d√©fini.

Pour utiliser les valeurs d'un variant, on peut les extraire √† l'aide de
`switch`, qui va _pattern-matcher_ pour nous permettre d'identifier et
d'extraire les valeurs.

```reason
/* Petit bonus, l'exemple utilise ReasonReact, mais on d√©taillera √ßa dans un prochain article */
let component = ReasonReact.statelessComponent("ChatMessage");

let make = (~message, _children) => {
  ...component,
  render: (_) =>
    <div>
      (switch message {
        | String(value) => ReasonReact.stringToElement(value)
        | Image({url: src, width, height}) => <img width height src />
        /* Si par m√©garde j'oublie un cas possible dans un switch, je vais avoir un joli warning du
          compiler qui me dira:
            This pattern-matching is not exhaustive.
            Here is an example of a value that is not matched:
              Emoji
        */
        | Emoji(value) =>
          <div style=(ReactDOMRe.Style.make(~fontSize="40px", ()))>
            (ReasonReact.stringToElement(value))
          </div>
      })
    </div>
};
```

En Reason, les _let bindings_ ont automatiquement le block parent comme scope,
on peut du coup √©crire des choses comme ceci :

```reason
let value = {
  let a = 1;
  let b = 2;
  a + b; /* la derni√®re expression est toujours retourn√©e par d√©faut */
};
/* let value : int = 3 */
a
/* Error: Unbound value a */
```

Voil√† pour l'introduction √† ReasonML, pour en d√©couvrir un peu plus je vous
invite √† aller lire la [documentation officielle](https://reasonml.github.io/).
On verra dans les prochains articles comment fonctionne
[ReasonReact](https://reasonml.github.io/reason-react) (les bindings Reason vers
React, avec quelques petites features sympathiques en plus), et comment
l'adopter incr√©mentalement dans sa codebase pour avoir du code plus s√ªr, et
_(spoilers)_, plus rapide que si vous l'√©criviez √† la main.

Bisous bisous.
